buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def VERSION_CODE = 4
def VERSION_NAME = "1.2"
def PACKAGE_NAME = "com.sakurafish.parrot.callconfirm"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion compile_sdk_version as int
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version as int
        targetSdkVersion target_sdk_version as int
        versionCode VERSION_CODE
        versionName VERSION_NAME
        applicationId PACKAGE_NAME
        manifestPlaceholders = [appName: "@string/app_name"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        debug {
            storeFile file("../debug.keystore")
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            manifestPlaceholders = [appName: "@string/app_name_staging"]
        }
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        preDexLibraries = false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':SakurafishLib')
    compile "com.android.support:support-v4:${support_lib_version}"
    compile "com.android.support:appcompat-v7:${support_lib_version}"
    compile "com.android.support:support-annotations:${support_lib_version}"
    compile "com.android.support:cardview-v7:${support_lib_version}"
    compile "com.google.firebase:firebase-ads:${googleplay_services_version}"
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.github.navasmdc:MaterialDesign:1.5@aar'
    compile 'com.patrickpissurno:ripple-effect:1.3.1'
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
    compile 'com.afollestad.material-dialogs:core:0.9.3.0'
}

def toApkFile(originalFile) {
    def namePrefix = "ParrotCallConfirm"
    def name = originalFile.name.replace(project.name,
            namePrefix + "-v" + android.defaultConfig.versionName)
    return new File(originalFile.parentFile, name)
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        output.outputFile = toApkFile(output.outputFile)
        if (output.packageApplication.outputFile != output.outputFile) {
            output.packageApplication.outputFile = toApkFile(output.packageApplication.outputFile)
        }
    }
}

apply plugin: 'com.google.gms.google-services'